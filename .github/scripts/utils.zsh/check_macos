autoload -Uz is-at-least log_info log_error log_status read_codesign

local macos_version=$(sw_vers -productVersion)

log_info 'Checking macOS version...'
if ! is-at-least 11.0 "${macos_version}"; then
  log_error "Minimum required macOS version is 11.0, but running on macOS ${macos_version}"
  return 2
else
  log_status "macOS ${macos_version} is recent"
fi

if (( ${+commands[git]} &&
    ${+commands[cmake]} &&
    ${+commands[ninja]} &&
    ${+commands[jq]} &&
    ${+commands[sha256sum]} )) {
  log_status "System has sufficient utilities installed, skipping HomeBrew step"
  return
}

log_info 'Checking for Homebrew...'
if (( ! ${+commands[brew]} )) {
  log_error 'No Homebrew command found. Please install Homebrew (https://brew.sh)'
  return 2
}

brew bundle --file "${SCRIPT_HOME}/.Brewfile"
rehash
